#! /usr/bin/perl

# pot_sum

use v5.20;
use warnings;
use experimental qw<signatures>;
use Getopt::Long qw<GetOptions>; use List::AllUtils qw<sum>;
GetOptions('n=i' => \(my $MaxGen = 20)) or die "$0: Unrecognized options\n";

sub plant_sum($pots, $pos) { sum map { $pos++ * ($_ eq 'x') } split //, $pots }

$_ = <> =~ /([#.]+)/ && $1 =~ tr/#./xo/r;  # Switch to o/x for empty/plant

my $rule = join '|', map { tr/#./xo/; /^(..)(.)(..)/; "(?<=$1)$2(?=$3)" } grep { /#$/ } <>;

my $offset = 0;  # Number of the leftmost pot in our string
for my $gen (1 .. $MaxGen)
{
  s/^o*/ooo/; $offset -= 3 - length $&;
  s/o*$/ooo/;

  state %prev;
  $prev{sum}  = plant_sum($_, $offset)  if $_ eq ($prev{pots} // '');
  $prev{pots} = $_;

  s/$rule/\u$&/gi;  # Mark all matching pots with upper-case
  s/[ox]/o/g;       # Any still lower-case become empty
  s/[OX]/x/g;       # Any upper-case get a plant

  if (defined $prev{sum})  # If prev iterations match, extrapolate answer:
  {
    my $sum = plant_sum($_, $offset);
    say $sum + ($sum - $prev{sum}) * ($MaxGen - $gen);
    exit;
  }
}
say plant_sum($_, $offset);
