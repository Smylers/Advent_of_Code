#! /usr/bin/perl

# highest_power

use v5.14; use warnings; no warnings qw<uninitialized>;

my ($serial_num) = @ARGV;
my (@sum, %max);
for my $x (1 .. 300)
{
  my $rack_id = $x + 10;
  for my $y (1 .. 300)
  {
    my $power = ($rack_id * $y + $serial_num) * $rack_id / 100 % 10 - 5;
    $sum[$x][$y] = $power + $sum[$x][$y - 1] + $sum[$x - 1][$y] - $sum[$x - 1][$y - 1];
  }
}
for my $size (0 .. 299)
{
  for my $x (1 .. 300 - $size)
  {
    for my $y (1 .. 300 - $size)
    {
      my $power = $sum[$x + $size][$y + $size] - $sum[$x + $size][$y - 1] - $sum[$x - 1][$y + $size] + $sum[$x - 1][$y - 1];
      $max{any} = {power => $power, pos => "$x,$y," . ($size + 1)} if !defined $max{any}{power} || $power > $max{any}{power};
    }
  }
}
say $max{any}{pos};
