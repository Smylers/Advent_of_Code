#! /usr/bin/perl

# highest_power

use v5.14; use warnings; use Syntax::Keyword::Junction qw<any>; use List::AllUtils qw<sum>;

my ($serial_num) = @ARGV;
my (@row, $highest_power, $highest_power_pos);
for my $x (reverse 1 .. 300)
{
  my $rack_id = $x + 10;
  for my $y (reverse 1 .. 300)
  {
    $row[0][$y] = ($rack_id * $y + $serial_num) * $rack_id / 100 % 10 - 5;
    unless ((any $x, $y) >= 299)
    {
      my $square_power = sum map { @{$row[$_]}[$y .. $y + 2] } 0 .. 2;
      if (!defined $highest_power || $square_power > $highest_power)
      {
        $highest_power = $square_power;
        $highest_power_pos = "$x,$y";
      }
    }
  }
  unshift @row, [];
  pop @row if @row > 3;
}
say $highest_power_pos;
