#! /usr/bin/perl

# largest_area

use v5.14; use warnings; use List::AllUtils qw<minmax any max>;

my @edge = map { {area_id => ++(state $area_id), coord => [/(\d+)/g]} } <>;
my @range;
for my $dimension (0..1) {
  @{$range[$dimension]}{qw<min max>} = minmax map { $_->{coord}[$dimension] } @edge;
}

my (%area_size, %done);
undef @area_size{1..$#edge};
while (@edge)
{
  foreach (@edge)
  {
    $area_size{$_->{area_id}}++ if exists $area_size{$_->{area_id}};
    $done{"@{$_->{coord}}"} = 1;
  }
  my %next_edge;
  foreach my $current (@edge)
  {
    foreach my $diff ([-1, 0], [+1, 0], [0, -1], [0, +1])
    {
      my @coord = map { $current->{coord}[$_] + $diff->[$_] } 0..1;
      next  if $done{"@coord"};
      if (any { $coord[$_] < $range[$_]{min} || $coord[$_] > $range[$_]{max} } 0..1)
      {
        delete $area_size{$current->{area_id}};
      }
      elsif (exists $next_edge{"@coord"})
      {
        if ($next_edge{"@coord"}{area_id} ne $current->{area_id})
        {
          delete $next_edge{"@coord"};
          $done{"@coord"} = 1;
        }
      }
      else
      {
        $next_edge{"@coord"} = {area_id => $current->{area_id}, coord => \@coord};
      }
    }
  }
  @edge = values %next_edge;
}
say max values %area_size;
