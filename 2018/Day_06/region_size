#! /usr/bin/perl

# region_size

use v5.14; use warnings; use List::AllUtils qw<minmax max>; use Syntax::Keyword::Junction qw<any>;

my (@coord, %dim, %area_size, $region_size);
while (<>)
{
  /(?<x>\d+), (?<y>\d+)/ or die "Unexpected input: $_";
  push @coord, {x => $+{x}, y => $+{y}};
  @{$dim{$_}}{qw<min max>} = minmax $+{$_} + 0, values %{$dim{$_}} for qw<x y>;
  $area_size{$#coord} = 0;
}

for my $x ($dim{x}{min} .. $dim{x}{max})
{
  my $side = $x == any values %{$dim{x}};
  for my $y ($dim{y}{min} .. $dim{y}{max})
  {
    my $edge = $side || $y == any values %{$dim{y}};
    my ($total, $min_dist, $min_area_id);
    while (my ($area_id, $pos) = each @coord)
    {
      $total += my $dist = abs ($pos->{x} - $x) + abs ($pos->{y} - $y);
      if (!defined $min_dist || $dist < $min_dist)
      {
        $min_dist = $dist;
        $min_area_id = $area_id;
      }
      elsif ($dist == $min_dist)
      {
        $min_area_id = '';
      }
    }
    delete $area_size{$min_area_id} if $edge;
    $area_size{$min_area_id}++      if exists $area_size{$min_area_id};
    $region_size++                  if $total <10e3;
  }
}
say 'largest area: ', max values %area_size;
say 'region size:  ', $region_size;
