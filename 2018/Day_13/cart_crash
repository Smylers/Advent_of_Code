#! /usr/bin/perl

# cart_crash

use utf8;
use v5.14;
use warnings;
use Sort::Key::Multi qw<ii_keysort>;

my %dir =
(
  '<' => {axis => 0, Δ => -1},
  '>' => {axis => 0, Δ => +1},
  '^' => {axis => 1, Δ => -1},
  'v' => {axis => 1, Δ => +1},
);
my (@track, %cart, %cart_pos);
while (<>)
{
  while (/([<v>^])/g)
  {
    (state $id)++;
    $cart{$id} = {id => $id, pos => [(pos) - 1, scalar @track], %{$dir{$1}}, jn_go => 1};
    $cart_pos{"@{$cart{$id}{pos}}"} = $id;
  }
  push @track, [split //];
}

while (keys %cart > 1)
{
  foreach (my @sorted = ii_keysort { @{$_->{pos}}[1, 0] } values %cart)
  {
    next if !$cart{$_->{id}};  # In case deleted earlier in this tick

    delete $cart_pos{"@{$_->{pos}}"};
    $_->{pos}[$_->{axis}] += $_->{Δ};

    if (my $other_cart_id = delete $cart_pos{"@{$_->{pos}}"})
    {
      say "Crash at $_->{pos}[0],$_->{pos}[1]";
      delete @cart{$_->{id}, $other_cart_id};
      next;
    }

    $cart_pos{"@{$_->{pos}}"} = $_->{id};
    my $at = $track[$_->{pos}[1]][$_->{pos}[0]];
    $_->{axis}  = 1 - $_->{axis}        if $at eq '/' || $at eq '\\' || $at eq '+' && $_->{jn_go} <  2;
    $_->{Δ}    *= -1                    if $at eq '/'                || $at eq '+' && $_->{jn_go} == $_->{axis};
    $_->{jn_go} = ($_->{jn_go} + 1) % 3 if $at eq '+';
  }
}
my ($final_cart) = values %cart;
say "Final cart at: $final_cart->{pos}[0],$final_cart->{pos}[1]";
