#! /usr/bin/perl

# cart_crash

use v5.14;
use warnings;
use Sort::Key::Multi qw<ii_keysort>;

my %movement =
(
  '<' => {axis => 0, delta => -1},
  '>' => {axis => 0, delta => +1},
  '^' => {axis => 1, delta => -1},
  'v' => {axis => 1, delta => +1},
);
my (@track, @cart);
while (<>)
{
  push @cart, {pos => [(pos) - 1, scalar @track], %{$movement{$1}}, junction_turn => 1} while /([<v>^])/g;
  push @track, [split //,  s/[<>]/-/gr =~ s/[v^]/|/gr];
}
my %cart_pos = map { ("@{$_->{pos}}" => 1) } @cart;

while (1)
{
  foreach (ii_keysort { @{$_->{pos}}[1, 0] } @cart)
  {
    $cart_pos{"@{$_->{pos}}"}--;
    $_->{pos}[$_->{axis}] += $_->{delta};
    if ($cart_pos{"@{$_->{pos}}"}++)
    {
      say "Crash at $_->{pos}[0],$_->{pos}[1]";
      exit;
    }
    my $at = $track[$_->{pos}[1]][$_->{pos}[0]];
    $_->{axis}   = 1 - $_->{axis} if $at eq '/' || $at eq '\\' || $at eq '+' && $_->{junction_turn} <  2;
    $_->{delta} *= -1             if $at eq '/'                || $at eq '+' && $_->{junction_turn} == $_->{axis};
    $_->{junction_turn} = ($_->{junction_turn} + 1) % 3 if $at eq '+';
  }
}
